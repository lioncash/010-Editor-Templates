//--------------------------------------
//--- 010 Editor v6.0.3 Binary Template
//
// Author  : Lioncash
// Revision: 1.0
// Purpose : Defines the layout of a Dolphin Emulator TAS movie file
//--------------------------------------

// TODO: Displaying the controller data would also be helpful.
//       Currently only the structs are provided, but not hooked up.

struct GameCubeInputData
{
    ubyte start       : 1 <name = "Start Button">;
    ubyte a           : 1 <name = "A Button">;
    ubyte b           : 1 <name = "B Button">;
    ubyte x           : 1 <name = "X Button">;
    ubyte y           : 1 <name = "Y Button">;
    ubyte z           : 1 <name = "Z Button">;

    ubyte dpad_up     : 1 <name = "D-Pad Up">;
    ubyte dpad_down   : 1 <name = "D-Pad Down">;
    ubyte dpad_left   : 1 <name = "D-Pad Left">;
    ubyte dpad_right  : 1 <name = "D-Pad Right">;

    ubyte l           : 1 <name = "L Trigger">;
    ubyte r           : 1 <name = "R Trigger">;

    ubyte disc_change : 1 <name = "Disc Changed">;
    ubyte reset       : 1 <name = "Reset Button">;
    ubyte reserved    : 2 <hidden = true>;

    ubyte l_pressure      <name = "L Trigger Analog", comment = "0: None | 255: Max">;
    ubyte r_pressure      <name = "R Trigger Analog", comment = "0: None | 255: Max">;

    ubyte analog_stick_x  <name = "Analog Stick X", comment = "Ranges from 1 to 255">;
    ubyte analog_stick_y  <name = "Analog Stick Y", comment = "Ranges from 1 to 255">;

    ubyte c_stick_x       <name = "C Stick X", comment = "Ranges from 1 to 255">;
    ubyte c_stick_y       <name = "C Stick Y", comment = "Ranges from 1 to 255">;
};

struct WiimoteControllerData
{
    ubyte payload_size          <name = "Payload Size">;
    ubyte payload[payload_size] <name = "Payload", comment = "Raw Wiimote data">;
};

struct DTM
{
    LittleEndian();

    enum <ubyte> CPUCore
    {
        CORE_INTERPRETER       = 0,
        CORE_JIT64             = 1,
        CORE_JITIL             = 2,
        CORE_JITARM64          = 4,
        CORE_CACHEDINTERPRETER = 5,
    };

    char   magic[4]              <name = "Magic">;
    char   game_id[6]            <name = "Game ID">;
    ubyte  is_wii_game           <name = "Is Wii Game">;
    ubyte  num_controllers       <name = "Number of Controllers">;
    ubyte  starts_from_savestate <name = "Starts From Savestate">;
    uint64 frame_count           <name = "Frame Count">;
    uint64 input_frames          <name = "Input Frames">;
    uint64 lag_frames            <name = "Lag Frames">;
    uint64 unique_id             <hidden = true>; // This isn't implemented.
    uint32 rerecord_count        <name = "Rerecord Count">;
    char   author[32]            <name = "Author">;
    char   video_backend[16]     <name = "Video Backend">;
    char   audio_emulator[16]    <name = "Audio Emulator">;
    ubyte  md5[16]               <name = "MD5">;
    uint64 recording_start_time  <name = "Recording Start Time", comment = "Seconds since 1970">;

    ubyte  using_saved_config    <name = "Using Saved Config", comment = "Additional values will show up when this is true.">;

    if (using_saved_config == 1)
    {
        ubyte   idle_skip       <name = "Idle Skip">;
        ubyte   dual_core       <name = "Dual Core">;
        ubyte   progressive     <name = "Progressive Scan">;
        ubyte   dsp_hle         <name = "DSP HLE">;
        ubyte   fast_disc_speed <name = "Fast Disc Speed">;
        CPUCore cpu_core        <name = "CPU Core">;
        ubyte   efb_access      <name = "EFB Access">;
        ubyte   efb_copies      <name = "EFB Copies">;
        ubyte   efb_copy_to_tex <name = "Copy EFBs To Texture", comment = "If zero, then this movie copies to RAM">;
        ubyte   efb_copy_cache  <name = "EFB Copy Cache">;
        ubyte   format_changes  <name = "Emulate Format Changes">;
        ubyte   emulate_xfb     <name = "Emulate XFB">;
        ubyte   real_xfb        <name = "Use Real XFB">;
        ubyte   memcard_present <name = "Memory Card Present">;
        ubyte   memcard_blank   <name = "Memory Card Was Blank">;
        ubyte   num_bongos      <name = "Number of Bongos">;
        ubyte   sync_gpu        <name = "Sync GPU Thread">;
        ubyte   netplay         <name = "Netplay Enabled">;
        ubyte   pal60           <name = "PAL60 Mode">;
        ubyte   reserved1[12]   <hidden = true>;
        char    second_disc[40] <name = "Name of Second Disc", comment = "This only applies for two-disc games">;
        ubyte   git_sha1[20]    <name = "SHA-1 of Git Revision">;
        uint32  dsp_irom_hash   <name = "DSP IROM Hash">;
        uint32  dsp_coef_hash   <name = "DSP COEF Hash">;
        uint64  tick_count      <name = "Tick Count">;
        ubyte   reserved2[11]   <hidden = true>;
    }
    else
    {
        ubyte ignore[118]       <hidden = true>;
    }
} dtm;